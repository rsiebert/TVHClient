plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "room.schemaLocation": "$projectDir/schemas".toString(),
                        "room.incremental"   : "true"
                ]
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            initWith buildTypes.release
        }
    }
    compileOptions {
        sourceCompatibility = rootProject.ext.javaCompatibility
        targetCompatibility = rootProject.ext.javaCompatibility
    }
    kotlinOptions.jvmTarget = rootProject.ext.javaCompatibility.toString()
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Support library basics
    implementation "androidx.appcompat:appcompat:$appCompatVersion"
    implementation "androidx.preference:preference-ktx:$prefKtxVersion"

    // Android Room
    def roomVersion = '2.4.0'
    implementation "androidx.room:room-runtime:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"

    // Logging library
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // Dependency injection
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
}
